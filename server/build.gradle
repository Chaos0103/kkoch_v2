plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

repositories {
    mavenCentral()
}

subprojects {
    group = 'com.ssafy'
    version = '2.0.0'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-test-fixtures'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    ext {
        springCloudVersion = "2024.0.0"
        set('snippetsDir', file("build/generated-snippets"))
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-actuator'

        //lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    bootJar { enabled = false }
    jar { enabled = true }
}

project(':user-service') {
    dependencies {
        implementation project(':common')
        implementation(testFixtures(project(":common")))
    }
}

project(':board-service') {
    dependencies {
        implementation project(':common')
        implementation(testFixtures(project(":common")))
    }
}
